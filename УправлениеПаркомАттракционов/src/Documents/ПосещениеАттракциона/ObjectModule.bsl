
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

// Формирует движения по регистру "АктивныеПосещения" и проверяет возможность проведения документа.
//
// Параметры:
//   Отказ - Булево - Флаг отмены проведения документа
//   Режим - РежимПроведенияДокумента - Режим проведения документа
Процедура ОбработкаПроведения(Отказ,Режим)

	// регистр АктивныеПосещения
	Движения.АктивныеПосещения.Записывать = Истина;
	Движение = Движения.АктивныеПосещения.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Основание = Основание;
	Движение.ВидАттракциона = ВидАттракциона(Аттракцион);
	Движение.КоличествоПосещений = 1;

	Движения.Записать();
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	АктивныеПосещенияОстатки.Основание КАК Основание
		|ИЗ
		|	РегистрНакопления.АктивныеПосещения.Остатки(, Основание = &Основание) КАК АктивныеПосещенияОстатки
		|ГДЕ
		|	АктивныеПосещенияОстатки.КоличествоПосещенийОстаток < 0";
	
	Запрос.УстановитьПараметр("Основание", Основание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "По одному билету можно проходить только 1 раз";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "Основание";
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


// Получает вид аттракциона по его ссылке.
//
// Параметры:
//   Аттракцион - СправочникСсылка.Аттракционы - Ссылка на аттракцион, для которого нужно получить вид
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ВидыАттракционов - Вид аттракциона
Функция ВидАттракциона(Аттракцион)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Аттракционы.ВидАттракциона 
		|ИЗ
		|	Справочник.Аттракционы КАК Аттракционы
		|ГДЕ
		|	Аттракционы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Аттракцион);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();
		
	Возврат Выборка.ВидАттракциона;	

КонецФункции

#КонецОбласти

#Область Инициализация

#КонецОбласти

#КонецЕсли
